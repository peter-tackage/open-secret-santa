apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '20.0.0'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        applicationId "com.moac.android.opensecretsanta"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 12
        versionName "3.1"
    }

    signingConfigs {
        release {}
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }

    // Refer to - https://gist.github.com/gabrielemariotti/6856974
    def Properties props = new Properties()
    def propFile = new File('open-secret-santa-app/signing.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))

        if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'signing.properties found but some entries are missing'
            android.buildTypes.release.signingConfig = null
        }
    } else {
        println 'signing.properties not found'
        android.buildTypes.release.signingConfig = null
    }

}

// Customs Maven repositories
repositories {
    maven {
        url "https://raw.github.com/peter-tackage/maven-repo/master/releases/"
    }
    maven {
        url "https://raw.github.com/peter-tackage/maven-repo/master/snapshots/"
    }
}
dependencies {

    compile project(':libraries:dagger-injections')

    compile 'com.android.support:support-v13:19.0.0'

    compile 'com.moac.drawengine:draw-engine:1.4.1'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.j256.ormlite:ormlite-core:4.48'

    compile 'com.squareup:otto:1.3.4'

    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.picasso:picasso:2.3.4'

    compile 'com.netflix.rxjava:rxjava-core:0.14.9'
    compile 'com.netflix.rxjava:rxjava-android:0.14.9'

    compile 'javamail-android:mail:1.0.0'
    compile 'javamail-android:additionnal:1.0.0'
    compile 'javamail-android:activation:1.0.0'

    compile 'com.makeramen:roundedimageview:1.5.0'

    androidTestCompile 'com.jayway.android.robotium:robotium-solo:4.3.1'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.0'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'

}



